import com.android.builder.core.BuilderConstants;

buildscript {
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.3'
    }
}
dependencies {
    repositories {
        mavenCentral()
    }
}

group = GROUP
version = VERSION_NAME

apply plugin: 'com.android.library'
apply plugin: 'maven'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.2'

    defaultConfig {
        minSdkVersion 19
        versionName VERSION_NAME
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    implementation 'com.android.support:support-annotations:27.1.1'

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.mockito:mockito-core:2.19.0'
    testImplementation 'org.easytesting:fest-assert-core:2.0M10'
    testImplementation 'org.robolectric:robolectric:4.0-alpha-3'
}

android.libraryVariants.all { variant ->
    def name = variant.buildType.name
    if (!BuilderConstants.DEBUG.equals(name)) {
        def task = project.tasks.create "jar${name.capitalize()}", Jar
        task.dependsOn variant.javaCompile
        task.from variant.javaCompile.destinationDir
        artifacts.add('archives', task);
    }
}

apply plugin: 'checkstyle'
checkstyle {
    configFile file('checkstyle.xml')
    ignoreFailures true
    showViolations false
    configProperties = [
            'checkstyle.suppressions.file': file('checkstyle-suppressions.xml'),
    ]
}

// Applying the checkstyle plugin adds multiple tasks but not
// one for checkstylin' against android sources. This task
// belows does the trick.
task checkstyle(type: Checkstyle) {
    configFile file('checkstyle.xml')
    source 'src/main/java'
    ignoreFailures false
    showViolations true
    include '**/*.java'
    exclude '**/gen/**'

    classpath = files()
}

apply from: 'gradle-mvn-push.gradle'

repositories {
    google()
}
